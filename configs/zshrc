# Autoload functions.
autoload -Uz colors
autoload -Uz vcs_info
autoload -Uz compinit

# Load modules.
zmodload zsh/complist

# Source plugins.
source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh 2>/dev/null

# Styles.
zstyle ':vcs_info:*' enable git cvs svn
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' unstagedstr '*'
zstyle ':vcs_info:*' stagedstr '+'
zstyle ':vcs_info:git:*' formats '%b %u %c'
zstyle ':vcs_info:git:*' actionformats '%b | %a %u %c'
zstyle ':completion:*' menu select

# Options.
setopt promptsubst
setopt globdots
setopt SHARE_HISTORY

# History setup.
[ -e ~/.cache/zsh ] || mkdir -p ~/.cache/zsh
HISTSIZE=10000
SAVEHIST=10000
HISTFILE=~/.cache/zsh/history

# Call functions that initialize/setup certain functionalities.
colors
compinit

# Prompt.
PROMPT=$'%B%F{green}%n@%m%F{white}:%F{blue}%~%b%f ${vcs_info_msg_0_}\n%# '

# Binds.
bindkey -v
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history
bindkey -s '^o' 'lfcd\n'
bindkey '^R' history-incremental-search-backward

# Zsh line editor custom widgets.
zle -N zle-keymap-select
zle -N zle-line-init

# Aliases.
alias ls='ls --color=auto --group-directories-first'
alias rm='rm -i'
alias su='sudo -i'
alias grep='grep --color=auto'
alias v="$EDITOR"
alias c=clear
alias l=ls
alias la='ls -a'
alias ll='ls -la'
alias d=cd
alias e=exit

# Functions.
zle-keymap-select() {
    # Echo terminal special terminal command to change the shape of the cursor depending on the keymap.
    case $KEYMAP in
        # Echo control caracters to make the cursor a block when in non insert mode.
        vicmd) echo -ne '\e[1 q';;

        # Echo control caracters to make the cursor a line when in insert mode or main the default if no vim mode activated.
        viins|main) echo -ne '\e[5 q';;
    esac
}

zle-line-init() {
    # When zsh starts echo special terminal command to show the cursor as a line
    echo -ne '\e[5 q'
}

preexec() {
    # Previous to zsh running a command echo special terminal command to show the cursor as a block, reseting it.
    echo -ne '\e[1 q'
}

precmd() { 
    vcs_info 
}

lfcd() {
    tmp="$(mktemp)"
    lf -last-dir-path="$tmp"
    if [ -f "$tmp" ]; then
        dir="$(cat "$tmp")"
        rm -f "$tmp"
        if [ -d "$dir" ]; then
            if [ "$dir" != "$(pwd)" ]; then
                cd "$dir"
            fi
        fi
    fi
}
